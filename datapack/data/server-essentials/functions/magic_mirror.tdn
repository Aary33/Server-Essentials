@on compile
  
define objective age
define objective var
define objective portal_cooldown 
define objective dimension

define objective mirror_cooldown
define objective mirror_damage
define objective tp_limit                   
var mirror_sound = resource <custom.magic_mirror.use>
var mirror_break_adv = resource <server-essentials:magic/symbiosis/break_magic_mirror>
var symbiosis_adv = resource <server-essentials:magic/symbiosis/craft_with_symbiosis>

#For items
var self = entity<@e[limit=1,sort=nearest,distance=0..0.1,type=!minecraft:player]>

#For marker, must be identical
var marker_tags = new nbt(["dummy", "marker"])
var marker_selector = entity<@e[tag=dummy,tag=marker,limit=1,sort=nearest]>


#Mirror timer before using again
var cooldown = 46
#Max uses for mirror before breaking
var max_uses = 25


define objective x.spawn
define objective y.spawn
define objective z.spawn

define objective id.L
define objective id.M
define objective owner.L
define objective owner.M
define objective thrower.L
define objective thrower.M


#Mirror Items
var repair_item = entity<@e[limit=1,sort=nearest,distance=0,nbt={
        Item:{
            id:"minecraft:ender_eye",
            Count:1b
        }
    }]>

define item broken_mirror minecraft:carrot_on_a_stick#9797002 {
    default name { "text" : "Broken Mirror", "color" : "gray", "italic" : false}

    default lore [
            [
                {"text" : "A frame of what once was.",
                    "color" : "dark_purple",
                    "italic" : false
                }]
            ]

    default nbt {
    }

    
}

define item magic_mirror minecraft:carrot_on_a_stick#9797001 {
    default name { "text" : "Ender Mirror", "color" : "aqua", "italic" : false}

    default lore [
                [
                {"text" : "Gaze into the eye to return home.",
                    "color" : "dark_purple",
                    "italic" : false
                }]
            ]

    default nbt {
        Enchantments:[{}]
    }
    
    #Mirror Behaviors

    function y_check {
        scoreboard players remove @s tp_limit 1
        
        #If current block is not air or limit is reached, teleport here
        unless block ~ ~ ~ minecraft:air
        set @s->tp_limit = 0
        
        if score @s tp_limit matches 0
        teleport @s ~ ~2 ~
        
        #If limit has not been reached, loop recursively
        if score @s tp_limit matches 1..
        positioned ~ ~-1 ~
        function /
    }       

    
    #Store spawn values in player
    function store_in_player
    {
        #Store spawn coordinates to player if bed
        if data entity @s SpawnX
        store result score @s x.spawn
            data get entity @s SpawnX
        if data entity @s SpawnY
        store result score @s y.spawn
            data get entity @s SpawnY
        if data entity @s SpawnZ
        store result score @s z.spawn
            data get entity @s SpawnZ
        
        #Store world spawn coordinates if no bed
        unless data entity @s SpawnX
        store result score @s x.spawn
            scoreboard players get #SPAWN_X var
        unless data entity @s SpawnY
        store result score @s y.spawn
            scoreboard players get #SPAWN_Y var
        unless data entity @s SpawnZ
        store result score @s z.spawn
            scoreboard players get #SPAWN_Z var
        
    }
    
    
    #Create and set ID's for marker
    function create_marker
    {
        #Summon marker
        summon $marker ~ ~ ~
        teleport $marker_selector[distance=0..0.1] @s
        
        #Set Owner
        set $marker_selector[distance=0..0.1]->owner.L = @s->id.L
        set $marker_selector[distance=0..0.1]->owner.M = @s->id.M
        
        
        tag @s add owner
        
    }
    
    #Store spawn values in marker
    function store_in_marker
    {
        store result score $marker_selector[distance=0..0.1] x.spawn
            scoreboard players get @s x.spawn
        store result score $marker_selector[distance=0..0.1] y.spawn
            scoreboard players get @s y.spawn
        store result score $marker_selector[distance=0..0.1] z.spawn
            scoreboard players get @s z.spawn
        
    }
    
    #Teleport marker
    function teleport_marker
    {
        in minecraft:overworld
        teleport @s 0 0 0
        
        store result entity @s Pos[0] 1
            scoreboard players get @s x.spawn
        store result entity @s Pos[1] 1
            scoreboard players get @s y.spawn
        store result entity @s Pos[2] 1
            scoreboard players get @s z.spawn
        
        at @s
        teleport @s ~0.5 ~ ~0.5
        
        #Second case: Spread
        at @s
        unless block ~ ~ ~ minecraft:air
        spreadplayers ~ ~ 0 5 false @s
        
        #Third case: World Spawn
        at @s
        unless block ~ ~ ~ minecraft:air
        function ${this.store_world_spawn}
        
        #Fourth case: World Spawn Spread
        at @s
        unless block ~ ~ ~ minecraft:air
        spreadplayers ~ ~ 0 5 false @s
        
        #Teleport if successful location
        at @s
        if block ~ ~ ~ minecraft:air
        in minecraft:overworld
        teleport @a[tag=owner] ~ ~ ~
        
        #Failure message
        at @s
        unless block ~ ~ ~ minecraft:air
        tellraw @a[tag=owner] ["",{"text":"The mirror stares back. No safe spawnpoint or worldspawn found.", "color":"gray","italic":true}]
        
        
        tag @a[tag=owner] remove owner
        
        kill @s
    }
    
    function store_world_spawn
    {
        store result entity @s Pos[0] 1
            scoreboard players get #SPAWN-X var
        store result entity @s Pos[1] 1
            scoreboard players get #SPAWN-Y var
        store result entity @s Pos[2] 1
            scoreboard players get #SPAWN-Z var
    }
    
    function destroy {
        playsound minecraft:block.glass.break player @a ~ ~ ~ 1 1.1 0
        replaceitem entity @s weapon.mainhand minecraft:air
        advancement grant @s until $mirror_break_adv
        loot give @s loot server-essentials:custom/mirror_loop_chance
    }
    
    function damage {
        
        store result score @s mirror_damage
        data get entity @s SelectedItem.tag.Damage
        
        var i = null
        for (i = 0; i < max_uses; i++) {
            var h = $i
            eval h++
            
            var new_dmg = nbt_value<{Damage: ${new tag_short(h)}}>
            
            if score @s mirror_damage matches $i
            replaceitem entity @s weapon.mainhand $magic_mirror$new_dmg 1
            
        }
        
        if score @s mirror_damage matches $max_uses
        function ${this.destroy}
    }
    
    function teleport {
        if entity @s[gamemode=!creative]
        function ${this.damage}
        
        effect give @s minecraft:resistance 1 5 true
        
        at @s
        summon $blast ~ ~1 ~
        
        function ${this.store_in_player}
        function ${this.create_marker}
        
        as $marker_selector[distance=0..0.1]
        at @s
        as @a
        if score @s id.L = $marker_selector[distance=0..0.1] owner.L
        if score @s id.M = $marker_selector[distance=0..0.1] owner.M
            function ${this.store_in_marker}
        
        as $marker_selector[distance=0..0.1]
        at @s
            function ${this.teleport_marker}
        
        at @s
        unless block ~ ~1 ~ minecraft:air
            spreadplayers ~ ~ 0 5 false @s
        
        at @s
        unless block ~ ~1 ~ minecraft:air
            teleport @s ~ 256 ~
        
        
        
        at @s
        summon $blast ~ ~1 ~
    }
    
    function animation {
        @tag tick
        
        #Player Check
        set @a->mirror_cooldown += 0
        
        
        as @a[scores={mirror_cooldown=1}]
        at @s
        raw ${"if entity @s[nbt={SelectedItem:{tag:" + magic_mirror.getItemTag() + "}}]"}
        if score @s mirror_cooldown matches 1
            function ${this.teleport}
        
        as @a[scores={mirror_cooldown=$cooldown}]
        at @s
            playsound $mirror_sound player @a ~ ~ ~ 1 0.8 0
        
        as @a[scores={mirror_cooldown=1..}]
        at @s expand {
            particle minecraft:end_rod ~ ~1 ~ 0.1 0.1 0.1 0.05 1 normal @a
            particle minecraft:dragon_breath ~ ~1 ~ 0.5 0.5 0.5 0.1 2 normal @a
            set @s->mirror_cooldown -= 1
        }
        
    }
    
    function craft {
        kill $repair_item
        /${"summon minecraft:item ~ ~ ~ {Motion:[0.0,0.5,0.0],PickupDelay:40s,PortalCooldown:300,Item:" + magic_mirror.getSlotNBT() + "}"}
        playsound minecraft:item.trident.thunder player @a ~ ~ ~ 1 1.9 0
        particle minecraft:totem_of_undying ~ ~1 ~ 0.2 0.5 0.2 0 10 normal @a
        
        #Target owner
        as @s
        at @s
        as @a
        if score @s id.L = $self thrower.L
        if score @s id.M = $self thrower.M
        advancement grant @s until $symbiosis_adv
        
        kill @s
    }
    
    function check_craft {
        @tag tick
        
        raw ${"as @e[nbt={Item:{tag:" + broken_mirror.getItemTag() + "}}]"}
        at @s expand {
            
            if score @s portal_cooldown matches 1..
            if entity $repair_item
            function ${this.craft}
            
            set @s->portal_cooldown = @s.PortalCooldown
        }

        
    }
    
    
    on used function use {
        if entity @s[scores={mirror_cooldown=0}]
            set @s->mirror_cooldown = $cooldown
        
    }
}

#Mirror Entities

define entity marker minecraft:area_effect_cloud {
    
    default name [
            "",
            {"text":"Teleport Marker"}
    ]
    
    default nbt {
            Duration:200,
            Tags:$marker_tags
    }

}

define entity blast minecraft:firework_rocket {
    default name [
            "",
            {"text":"Teleport Blast"}
    ]
    
    default nbt {
        LifeTime:1,
        FireworksItem:
        {
            id:firework_rocket,
            Count:1b,
            tag:
            {
                Fireworks:{
                    Flight:1b,
                    Explosions:[
                        {
                            Type:0b,
                            Flicker:0b,
                            Trail:0b,
                            Colors:[I;12156866],
                            FadeColors:[I;1780496]
                        }
                    ]
                }
            }
        }
    }
    
    ticking function particle {
        particle minecraft:portal ~ ~ ~ 0 0 0 1 100 normal @a
    }
}
